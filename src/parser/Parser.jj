/* adder.jj Adding up numbers */
options 
{
    STATIC    = false ;
    LOOKAHEAD = 4;
}
PARSER_BEGIN(Parser)

package parser;
import parser.*;
import java.util.HashMap;
import exception.WrongTypeException;

/**
 * <b>Parser for user Program<b>
 * @author Renato Cordeiro Ferreira
 * @author Vinicius Nascimento Silva
 */
public class Parser 
{
    HashMap<String,String> symbol = new HashMap<String,String>();
    
    public static void main( String[] args )
        throws ParseException, 
               TokenMgrError, 
               WrongTypeException 
    {
        Parser parser = new Parser(System.in);
        parser.Start(); 
    }
}

PARSER_END(Parser)

SKIP : { " " | "\n" | "\r" | "\r\n"}
TOKEN : {< LBRACE : "(" >}
TOKEN : {< RBRACE : ")" >}

TOKEN : {< NUMBER : (["0"-"9"])+  ("." (["0"-"9"])+)? >}
TOKEN : {< STRING : ( ("\"") (~["\""])* ("\"")) >}

TOKEN : {< SIGN : "+"  | "-"  >}
TOKEN : {< OP   : "*"  | "/" | "%" >}
TOKEN : {< CMP  : "==" | "!=" | "<" | "<=" | ">=" | ">" >}
TOKEN : {< ATTR : "="  | "+=" | "-=" | "*=" | "/=" | "%=" >}

TOKEN : {< WORD   : (["A"-"Z"] |  ["a"-"z"] | "_")+ >}
TOKEN : {< EOL : ";" >}
/**
 * 
 * 
 * 
 */
 
/**
 * Defines program expressions.
 */ 
void Start() 
    throws WrongTypeException :
{
    String s;
}    
{
    (
        (   
            s = expression()
            { insert("POP"); }
        )?
        <EOL>
        { say("--------------------------------"); }
    )*
}

/**
 * Expression is a comparison of Arit's.
 */
String expression() 
    throws WrongTypeException :
{
    Token   t;
    String s = "";
}
{
    s = arit()
    (
        t = <CMP>
        { checkTypeError(s); }            
        s = arit()
        { checkTypeError(s); }
        { insert(cmp(t)); }
    )*
    { return s; }
}

/**
 * Arit is a sum of terms.
 */
String arit() 
    throws WrongTypeException :
{
    Token   t;
    String  s = "";
}
{
    s = term()
    (
        t = <SIGN>            
        { checkTypeError(s); }
        s = term() 
        { checkTypeError(s); }
        { insert(sign(t)); }
    )*
    { return s; }
}

/**
 * Term is a product of elements.
 */
String term () 
    throws WrongTypeException :
{
    Token  t;
    double value;
    String s = "";
}
{
    s = element()
    (
        t = <OP>
        { checkTypeError(s); }
        s = element()
        { checkTypeError(s); }
        { insert(op(t)); }
    )*
    { return s; }
}

/**
 * Element is either a factor or a signed factor.
 */
String element() 
    throws WrongTypeException :
{
    Token t;
    String s = "";;
}
{
    (s = factor()
    { return s; })
    |
    (
        t = <SIGN>
        s = factor()
        { checkTypeError(s); }
        { unarySign(t); }
        { return s; }
    )
    {return s;}
    
}

/**
 * Factor is either a attribution, a variable, 
 * a number or a braced expression.
 */
String factor ()  
    throws WrongTypeException :
{
    Token t, v;
    double   value;
    String s = "";
}
{    
    (
        t = <WORD> 
        v = <ATTR>
        s = expression()
        { attr(t, v, s);
            return s; }
    )
    |
        t = <WORD>
        { s = get(t); }
        { return s; }
    |
    (
        t = <STRING>
        { PUSH(t.image);     }
        { return "string"; }
    )
    |
    (
        t = <NUMBER>
        { value = atof(t); }
        { PUSH(value);     }
        { return "number"; }
    )
    |
    (
        <LBRACE> 
        s = expression()
        <RBRACE>
        { return s; }
    )
    
}

/**
 * Auxiliar function for 
 * pushing in the assembly.
 * @param o Pushed object
 */
void PUSH(Object o) :
{}
{{    
    insert("PUSH ", o);
}}

/**
 * Print string representation of | "%"
 * a variable number of objects.
 * @param obj List of objects
 */
void insert(Object ... obj) :
{
    String s;
}
{{
    for(Object o: obj)
    {
        s = (o == null)? "null" : o.toString();
        System.out.print(s);
    }
    System.out.println();
}}

/**
 * Insert a attribution of a double variable.
 * @param var  Token of the variable
 * @param attr Token of the attribution  
 */
void attr(Token var, Token attr, String type) 
    throws WrongTypeException:
{ String s; }
{{
    s = var.image;
    if(!symbol.containsKey(s))
        insert("ALOC ", "[", s, "]");
    symbol.put(s, type);
    switch(attr.image)
    {
        case "="  : break;
        case "+=" : checkTypeError(type);
                    insert("GET ", "[", s, "]"); 
                    insert("ADD"); 
                    break;
        
        case "-=" : checkTypeError(type);
                    insert("GET ", "[", s, "]");
                    insert("SWAP"); 
                    insert("SUB"); 
                    break;
                    
        case "*=" : checkTypeError(type);
                    insert("GET ", "[", s, "]"); 
                    insert("MUL"); 
                    break;
                    
        case "/=" : checkTypeError(type);
                    insert("GET ", "[", s, "]"); 
                    insert("SWAP");
                    insert("DIV"); 
                    break;
                    
        case "%=" : checkTypeError(type);
                    insert("GET ", "[", s, "]"); 
                    insert("SWAP");
                    insert("MOD"); 
                    break;
    } 
    
    insert("SET ", "[", s, "]"); insert("GET ", "[", s, "]");
}}

/**
 * Insert a GET for a variable.
 * @param var  Token of the variable
 */
String get(Token var) 
    throws WrongTypeException :
{ String s; }
{{
    s = var.image;
    if(!symbol.containsKey(s))
    {
        symbol.put(s, "number");
        insert("ALOC ", "[", s, "]");
        insert("PUSH 0");
        insert("SET ", "[", s, "]");
    }
    insert("GET ", "[", s, "]");
    return symbol.get(s);
}}

/**
 * Auxiliar function - cmp.
 * @param  t Token with the comparison
 *           operator
 * @return String with comparison operator
 */
String cmp (Token t) :
{}
{{
    switch(t.image)
    {
        case "==": return "EQ";
        case "!=": return "NE";
        case "<" : return "LT";
        case "<=": return "LE";
        case ">=": return "GE";
        case ">" : return "GT";
        default  : return "\n"; // Invalid operator
    } 
}}

/**
 * Auxiliar function - sign.
 * @param  t Token with the sign
 * @return Sign of a term
 */
String sign (Token t) :
{}
{{
    return t.image.equals("+") ? "ADD" : "SUB"; 
}}

/**
 * Auxiliar function - op.
 * @param  t Token with the operation
 * @return Operation of a factor
 */
String op (Token t) :
{}
{{
    switch (t.image)
    {
        case "*": return "MUL";
        case "/": return "DIV";
        case "%": return "MOD";
     }   
    return t.image;
}}

/**
 * insert a additive inverse of a factor 
 * @param  t Token with the sign
 */
void unarySign (Token t) :
{}
{{
    if(t.image.equals("-"))
    {
        insert("PUSH -1");
        insert("MUL");
    } 
}}

/**
 * Auxiliar function - atof.
 * @param  t Token with a double
 * @return Double value of the token
 */
double atof(Token t) :
{}
{{    
    return Double.parseDouble(t.image);
}}

/**
 * Auxiliar function - say.<br>
 * Abbreviation of "System.out.println" 
 * with debugger purpouses.
 * @param o Object to be printed
 */
private void say(Object o) :
{
    String s;
}
{{
    s = (o == null)? "null" : o.toString();
    System.out.println(s);
}}

private void checkTypeError(String s)
    throws WrongTypeException :
{}
{{
    if (s != null)
        if(!s.equals("number"))
            throw new WrongTypeException("number");
            
}}
